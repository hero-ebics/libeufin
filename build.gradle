// This file is in the public domain.

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    // id 'org.jetbrains.kotlin.jvm' version '1.5.30'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    // id 'signing'
}

group = 'tech.libeufin'
version = '1.0.3-hero'

if (!JavaVersion.current().isJava11Compatible()){
    throw new GradleException(
        "This build must be run with java 11 " +
        "or later (your version is java ${JavaVersion.current()})")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'


    ext.set("ktor_version", "2.2.1")
    ext.set("ktor_auth_version", "1.6.8")
    ext.set("exposed_version", "0.32.1")

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

idea {
    module {
        excludeDirs += file("frontend")
    }
}

task versionFile() {
    new File("${projectDir}/util/src/main/resources", "version.txt").text = getRootProject().version
}

// See: https://stackoverflow.com/questions/24936781/gradle-plugin-project-version-number
task libeufinVersion {
    doLast {
        println project.version
    }
}

task replaceVersionCli(type: Copy) {
    from file("cli/bin/libeufin-cli")
    into file("${project.buildDir}/generated/python")
    filter(ReplaceTokens, tokens: [version: getRootProject().version])
}

classes {
    dependsOn versionFile
    dependsOn replaceVersionCli
}

task execArch(type: Zip) {
    dependsOn versionFile
    dependsOn replaceVersionCli
    evaluationDependsOn("nexus")
    evaluationDependsOn("sandbox")
    def topDir = "${getRootProject().name}-${getRootProject().version}"
    archiveFileName = "${topDir}.zip"
    subprojects.each {
        if (it.name == "nexus" || it.name == "sandbox") {
            Task t = it.tasks.getByName("installShadowDist")
            dependsOn(t) // invokes the task 't'
        }
    }
    from("nexus/build/install/nexus-shadow") {
        include("**/libeufin-nexus")
        include("**/*.jar")
    }
    from("sandbox/build/install/sandbox-shadow") {
        include("**/libeufin-sandbox")
        include("**/*.jar")
    }
    from("${project.buildDir}/generated/python") {
      include("libeufin-cli")
      rename { "bin/libeufin-cli" }
    }
    into(topDir)
}
